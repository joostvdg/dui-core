plugins {
    id 'java'
    id 'application'
}


// JAVA VERSION
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

import java.util.regex.Matcher;

dependencies {
    implementation project(':server')
    implementation project(':client')

    implementation 'com.github.joostvdg.dui:dui-api:1.0.5'
    implementation 'com.github.joostvdg.dui:dui-lib-logging:1.0.1'

    testImplementation 'junit:junit:4.12'
}

ext.moduleName = 'com.github.joostvdg.dui.core'

// tag::run[]
// Define the main class for the application
mainClassName = "$moduleName/com.github.joostvdg.dui.core.DockerApp" // <1>

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
            '--module-path', classpath.asPath,
            '--module', mainClassName // <2>
        ]
        classpath = files()
    }
}
// end::run[]

// tag::startScripts[]
startScripts {
    inputs.property("moduleName", moduleName)
    doFirst {
        classpath = files()
        defaultJvmOpts = [
            '--module-path', 'APP_HOME_LIBS',  // <1>
            '--module', mainClassName
        ]
    }
    doLast{
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('$APP_HOME/lib'))

        def batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('%APP_HOME%\\lib'))
    }
}
// end::startScripts[]
